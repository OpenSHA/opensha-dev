/*
 * opensha-dev dependencies
 */

apply from: '../opensha-commons/build-common.gradle'

dependencies {

    compileOnly 'org.apache.pdfbox:pdfbox:2.0.6'
    compileOnly 'org.apache.commons:commons-collections4:4.1'
    compileAndResource 'org.apache.commons:commons-compress:1.15'
    compileOnly 'com.atlassian.commonmark:commonmark:0.10.0'
    compileOnly 'com.atlassian.commonmark:commonmark-ext-gfm-tables:0.10.0'
    compileOnly 'com.atlassian.commonmark:commonmark-ext-heading-anchor:0.10.0'

    /* for the CMU group */
    compileOnly 'org.apache.activemq:activemq-all:5.15.2'
    compileOnly 'com.fasterxml.jackson.core:jackson-annotations:2.8.11'
    compileOnly 'com.fasterxml.jackson.core:jackson-core:2.8.11'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.8.11'
    compileOnly 'org.mongodb:mongo-java-driver:3.6.2'
    compileOnly 'org.mongodb.morphia:morphia:1.3.2'
 
    /* no remote repo */
    compileOnly files('lib/EventWebService.jar')
    compileOnly files('lib/ProductClient.jar')

    compileOnly project(path: ':opensha-apps', configuration: 'compileOnly')
    compileAndResource project(path: ':opensha-apps')

    testCompile 'junit:junit:4.12'
}

task appOAFJar(type: Jar) {
    baseName = 'AftershockGUI-current-'+getDate()
    from { configurations.compileAndResource.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude { it.path.contains('META-INF') }
        }
    }}
    from { configurations.compileOnly.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude { it.path.contains('META-INF') }
        }
    }}
	manifest {
    	attributes(
			'Class-Path': configurations.compileAndResource.collect { it.getName() }.join(' '),
			'Main-Class': 'scratch.aftershockStatistics.AftershockStatsGUI'
		)
	}
    with jar
}

task oefJar(type: Jar) {
    baseName = project.name + '-oef'
    from { configurations.compileAndResource.collect {
        it.isDirectory() ? it : zipTree(it)
    }}
    with jar
}
